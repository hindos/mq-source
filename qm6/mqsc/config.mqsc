ALTER QMGR DEFCLXQ(CHANNEL)

* Define LDAP config
DEFINE AUTHINFO(USE.LDAP) + 
AUTHTYPE(IDPWLDAP) +
CONNAME('ldap-service.ldap(389)') +
LDAPUSER('cn=admin,dc=ibm,dc=com') LDAPPWD('admin') +
SECCOMM(NO) +
USRFIELD('uid') +
SHORTUSR('uid') +
BASEDNU('ou=people,dc=ibm,dc=com') +
AUTHORMD(SEARCHGRP) +
BASEDNG('ou=groups,dc=ibm,dc=com') +
GRPFIELD('cn') +
CLASSGRP('groupOfUniqueNames') +
FINDGRP('uniqueMember') +
REPLACE

ALTER QMGR CONNAUTH(USE.LDAP)

REFRESH QMGR TYPE(CONFIGEV) OBJECT(AUTHINFO)

SET CHLAUTH('*') TYPE(BLOCKUSER) USERLIST(*MQADMIN) WARN(YES) ACTION(REPLACE)


REFRESH SECURITY

SET AUTHREC OBJTYPE(QMGR) GROUP('mqadmins') AUTHADD(ALL)
SET AUTHREC OBJTYPE(QUEUE) PROFILE('**') GROUP('mqadmins') AUTHADD(ALL)
SET AUTHREC OBJTYPE(CHANNEL) PROFILE('**') GROUP('mqadmins') AUTHADD(ALL)

REFRESH QMGR TYPE(CONFIGEV) OBJECT(AUTHREC)



DEFINE CHANNEL('TEST.APP.SVRCONN') CHLTYPE(SVRCONN) MCAUSER('app') SSLCAUTH(OPTIONAL) SSLCIPH('ECDHE_RSA_AES_256_CBC_SHA384') TRPTYPE(TCP) REPLACE

* Developer channel authentication rules
SET CHLAUTH('*') TYPE(ADDRESSMAP) ADDRESS('*') USERSRC(NOACCESS) DESCR('Back-stop rule - Blocks everyone') ACTION(REPLACE)
SET CHLAUTH('TEST.APP.SVRCONN') TYPE(ADDRESSMAP) ADDRESS('*') USERSRC(CHANNEL) CHCKCLNT(REQUIRED) DESCR('Allows connection via APP channel') ACTION(REPLACE)
SET CHLAUTH('DEV.ADMIN.SVRCONN') TYPE(BLOCKUSER) USERLIST('nobody') DESCR('Allows admins on ADMIN channel') ACTION(REPLACE)
SET CHLAUTH('DEV.ADMIN.SVRCONN') TYPE(USERMAP) CLNTUSER('admin') USERSRC(CHANNEL) DESCR('Allows admin user to connect via ADMIN channel') ACTION(REPLACE)
* We will need to set the *MQADMIN rule to WARN(YES) probably

SET AUTHREC PROFILE('self') GROUP('apps') OBJTYPE(QMGR) AUTHRMV(ALL)
SET AUTHREC PROFILE('self') GROUP('apps') OBJTYPE(QMGR) AUTHADD(CONNECT,INQ)

* Define test queue used to validate queue manager by tekton pipeline
DEFINE QLOCAL(TEST.QUEUE.V1) DEFPSIST(YES) REPLACE
SET AUTHREC PROFILE(TEST.QUEUE.V1) GROUP('apps') OBJTYPE(QUEUE)  AUTHADD(PUT, GET, PASSALL, BROWSE, INQ)

REFRESH QMGR TYPE(CONFIGEV) OBJECT(AUTHREC)
REFRESH SECURITY
